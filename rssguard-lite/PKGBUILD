# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Alois Nespor <info@aloisnespor.info>
# Contributor: speps <speps dot aur dot archlinux dot org>

_pkgname=rssguard
_commit=459f5a8a4a7621f3fc578cfa49ef18bd46121644
pkgname=rssguard-lite
pkgver=4.8.5.r13.g459f5a8
pkgrel=1
pkgdesc='Simple (yet powerful) Qt feed reader without WebEngine support'
arch=(x86_64)
url='https://github.com/martinrotter/rssguard'
license=(GPL-3.0-only)
depends=(gcc-libs
         glibc
         libglvnd
         mpv
         qt6-5compat
         qt6-base
         qt6-declarative
         qt6-multimedia
         sqlite)
makedepends=(cmake
             git
             qt6-tools
             qt6-webengine)
optdepends=('clang: Support for beautification of message filter scripts'
            'mariadb-libs: Support for MariaDB-based data storage')
conflicts=(rssguard)
provides=(rssguard=$pkgver)
replaces=(rssguard-nowebengine)
source=(git+https://github.com/martinrotter/rssguard#commit=$_commit)
sha256sums=('ba37cdd63e8ac82996a9095b6d0ea6b6dbb64b80390d297dc350086445f4219c')

pkgver() {
  git -C "$srcdir/$_pkgname" describe --long --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  # revert: save column width of feed view and make it changeable
  git -C "$srcdir/$_pkgname" revert -n 2ed50e55f99ea1067fc64a8af18276cd1f737151
}

build() {
  cmake -B build-lite -S $_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_LITE=OFF
  cmake --build build-lite
}

package() {
  DESTDIR="$pkgdir" cmake --install build-lite
}
