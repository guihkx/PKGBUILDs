# Maintainer: gbr <gbr@protonmail.com>
_pkgname=ratt
pkgname=ratt-git
pkgver=r42.cf2c8e4
pkgrel=1
pkgdesc='Convert websites into RSS/Atom feeds'
arch=('x86_64')
url='https://gitlab.com/microo8/ratt'
license=('custom')
makedepends=('git' 'go')
conflicts=('ratt')
provides=('ratt')
source=("$_pkgname::git+$url.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$_pkgname"
  go build \
    -o "$_pkgname" \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags \"${LDFLAGS}\"" \
    .
}

check() {
  cd "$_pkgname"
  go test -v ./...
}

package() {
  cd "$_pkgname"
  install -Dm755 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 'LICENSE' "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
