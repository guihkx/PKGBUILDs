# Maintainer: gbr <gbr@protonmail.com>
_pkgname=mt
pkgname=mt-git
pkgver=1.0.13.r2.g62fc39e
pkgrel=1
pkgdesc='Generate video contact sheets easily'
arch=('x86_64')
url='https://github.com/mutschler/mt'
license=('GPL3')
makedepends=('git' 'go')
depends=('ffmpeg' 'glibc')
source=("$_pkgname::git+$url.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v -o "$_pkgname" .
}

package() {
  cd "$_pkgname"
  install -Dm755 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}
