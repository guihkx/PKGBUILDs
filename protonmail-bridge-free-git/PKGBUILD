pkgbase=protonmail-bridge-free-git
_pkgbase=proton-bridge
pkgname=(
  protonmail-bridge-core-free-git
  protonmail-bridge-free-git
)
pkgver=3.17.0.r107.ge6ffd55
pkgrel=1
pkgdesc="Integrate ProtonMail paid account with any program that supports IMAP and SMTP (with free user support)"
arch=(x86_64)
url="https://github.com/mnixry/proton-bridge"
license=(GPL-3.0-only)
makedepends=(
  abseil-cpp
  cmake
  gcc-libs
  git
  glib2
  glibc
  go
  grpc
  libsecret
  protobuf
  qt6-base
  qt6-declarative
  qt6-svg
  sentry-native
)
options=(!lto)
source=(
  "git+$url.git"
  "protonmail-bridge.service"
  "remove-vcpkg-dependency.patch"
  "remove-failing-test.patch"
  "fix-wayland-icon.patch::https://github.com/ProtonMail/proton-bridge/commit/75e7bba6e62ef4b0a58334f2bcc7c56b7769c69c.patch"
)
sha256sums=('SKIP'
            'd51fb6efde22354d84b894b979a685d06fcc9cf3040d999d1cd6ebf5242a5043'
            '436a9a293424fb7d48a8fd61fff7d63985dec56d6170cd2fbae24fe03ef98136'
            'c3a72d39a76abf3ee1567c5c44aac130fb327f4f5c93e426fef7ae3b4bf7f04f'
            '45a68688cf2a06539d60e3e35b112a67319c58c23c35ec4cf3712d2f0cb50cb2')

_archive="$_pkgbase"

pkgver() {
  cd "$_pkgbase"
  git remote add upstream https://github.com/ProtonMail/proton-bridge.git || true
  git fetch upstream --tags
  git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_archive"

  # We want to dynamically link system libraries instead of building
  # dependenceies from source using vcpkg and linkng them statically.
  patch --forward --strip 1 --input "$srcdir/remove-vcpkg-dependency.patch"

  # Remove a single test that fails on build.archlinux.org but not locally -
  # not sure why this is the case.
  patch --forward --strip 1 --input "$srcdir/remove-failing-test.patch"

  # Set the desktop filename in initQtApplication() to fix icon display
  # on Wayland.
  # See https://github.com/ProtonMail/proton-bridge/issues/443
  # and https://github.com/ProtonMail/proton-bridge/pull/497
  patch --forward --strip 1 --input "$srcdir/fix-wayland-icon.patch"

  # Use system Qt rather than bundling it.
  cat /dev/null > internal/frontend/bridge-gui/bridge-gui/DeployLinux.cmake

  # Avoid downloading Go dependencies in build() & check() by vendoring them
  # here.
  GOFLAGS="-mod=readonly" go mod vendor -v
}

build() {
  cd "$_archive"

  pushd utils/
  ./credits.sh bridge
  popd

  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -mod=vendor -modcacherw"
  export GOPATH="$srcdir"

  local tag="$(git describe --tag --abbrev=0)"
  local commit="$(git rev-parse HEAD)"

  local ld_flags=" \
    -compressdwarf=false \
    -linkmode=external \
    -X 'github.com/ProtonMail/proton-bridge/v3/internal/constants.BuildEnv=Arch Linux' \
    -X 'github.com/ProtonMail/proton-bridge/v3/internal/constants.FullAppName=Proton Mail Bridge' \
    -X github.com/ProtonMail/proton-bridge/v3/internal/constants.BuildTime=NOTSET \
    -X github.com/ProtonMail/proton-bridge/v3/internal/constants.Revision=$commit \
    -X github.com/ProtonMail/proton-bridge/v3/internal/constants.Tag=$tag \
    -X github.com/ProtonMail/proton-bridge/v3/internal/constants.Version=${pkgver%%.r*} \
  "
  go build -v \
    -ldflags "$ld_flags" \
    -o bridge \
    ./cmd/Desktop-Bridge/

  cmake \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev \
    -DBRIDGE_APP_FULL_NAME='Proton Mail Bridge' \
    -DBRIDGE_VENDOR='Proton AG' \
    -DBRIDGE_REVISION="$commit" \
    -DBRIDGE_TAG="$tag" \
    -DBRIDGE_DSN_SENTRY= \
    -DBRIDGE_BUILD_ENV='Arch Linux' \
    -DBRIDGE_APP_VERSION=${pkgver%%.r*} \
    -S internal/frontend/bridge-gui/bridge-gui \
    -B build
  cmake --build build
}

check() {
  cd "$_archive"

  # These tests are expecting ports 1143 & 1144 (default and fallback ports for
  # IMAP) and ports 1025 & 1026 (default and fallback ports for SMTP) to be
  # free for some specific tests scenarios.
  # If those ports are already in use by any other service(s) (e.g. there's
  # already a running protonmail-bridge instance on those default ports in your
  # build env/machine), tests will fail.
  go test -v ./...

  cmake --build build --target bridgepp-test
  ./build/bridgepp/bridgepp-test
}

package_protonmail-bridge-core-free-git() {
  pkgdesc="$pkgdesc (core executable and daemon)"
  depends=(
    glib2
    glibc
    libsecret
  )
  optdepends=(
    'gnome-keyring: gnome-keyring support'
    'org.freedesktop.secrets: Applications that support Freedesktop secrets api'
    'pass: pass support'
  )
  conflicts=('protonmail-bridge-core')
  replaces=('protonmail-bridge-core')

  cd "$_archive"

  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  install -Dm644 -t "$pkgdir/usr/lib/systemd/user" "$srcdir/protonmail-bridge.service"

  # The "core" bin is expected to be called "bridge" but that's a too generic
  # name to have under /usr/bin (it conflicts with iproute2 for instance).
  install -Dm755 bridge "$pkgdir/usr/lib/protonmail/bridge/bridge"
  install -dm755 "$pkgdir/usr/bin/"
  ln -s /usr/lib/protonmail/bridge/bridge "$pkgdir/usr/bin/protonmail-bridge-core"
}

package_protonmail-bridge-free-git() {
  pkgdesc="$pkgdesc (Qt desktop application)"
  depends=(
    protonmail-bridge-core-free-git

    abseil-cpp
    gcc-libs
    glibc
    grpc
    hicolor-icon-theme
    protobuf
    qt6-base
    qt6-declarative
    qt6-svg
    sentry-native
  )
  optdepends=(
    'qt6-wayland: Wayland support'
  )
  conflicts=('protonmail-bridge')
  replaces=('protonmail-bridge')

  cd "$_archive"

  install -Dm644 -t "$pkgdir/usr/share/applications" dist/proton-bridge.desktop
  install -Dm644 dist/bridge.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/protonmail-bridge.svg"

  # The "gui" bin is expected to be called "bridge-gui" but that's a too
  # generic name to have under /usr/bin.
  install -Dm755 build/bridge-gui "$pkgdir/usr/lib/protonmail/bridge/bridge-gui"
  install -dm755 "$pkgdir/usr/bin/"
  ln -s /usr/lib/protonmail/bridge/bridge-gui "$pkgdir/usr/bin/protonmail-bridge"
}
