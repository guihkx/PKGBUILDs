# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgname=mutter
pkgver=40.0+103+ga49dc7839
pkgrel=1
pkgdesc="A window manager for GNOME"
url="https://gitlab.gnome.org/GNOME/mutter"
arch=(x86_64)
license=(GPL)
depends=(dconf gobject-introspection-runtime gsettings-desktop-schemas
         libcanberra startup-notification zenity libsm gnome-desktop upower
         libxkbcommon-x11 gnome-settings-daemon libgudev libinput pipewire
         xorg-xwayland graphene libxkbfile)
makedepends=(gobject-introspection git egl-wayland meson xorg-server)
checkdepends=(xorg-server-xvfb pipewire-media-session)
provides=(libmutter-8.so)
groups=(gnome)
install=mutter.install
_commit=a49dc78396bda45da43ac111ffdceeca2892800d # master
source=("git+https://gitlab.gnome.org/GNOME/mutter.git#commit=$_commit"
        "0001-window-actor-Special-case-shaped-Java-windows.patch"
        "1826-clutter-frame-clock-Apply-error-diffusion-dithering-to-dispatch-times.patch"
)
sha256sums=('SKIP'
            'fa45ccba4f5eca6f54030274d6d8c30c10d7e8a68ea592e70e3469654a41b8e7'
            'b4dc265d9ef3baaead4392a2b5dfc8812822786c2627779a99930cfeef3b7ddd')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 < "${srcdir}/0001-window-actor-Special-case-shaped-Java-windows.patch"
  patch -Np1 < "${srcdir}/1826-clutter-frame-clock-Apply-error-diffusion-dithering-to-dispatch-times.patch"
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"
  arch-meson $pkgname build \
    -D egl_device=true \
    -D wayland_eglstream=true \
    -D installed_tests=false \
    -D profiler=false
  meson compile -C build
}

_check() (
  mkdir -p -m 700 "${XDG_RUNTIME_DIR:=$PWD/runtime-dir}"
  glib-compile-schemas "${GSETTINGS_SCHEMA_DIR:=$PWD/build/data}"
  export XDG_RUNTIME_DIR GSETTINGS_SCHEMA_DIR

  pipewire &
  _p1=$!

  pipewire-media-session &
  _p2=$!

  trap "kill $_p1 $_p2; wait" EXIT

  meson test -C build --print-errorlogs
)

check() {
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local +iglx -noreset' \
    bash -c "$(declare -f _check); _check"
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
