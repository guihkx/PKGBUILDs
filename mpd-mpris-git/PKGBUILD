# Maintainer: Nguyen Pham Cao <natsukagami at gmail dot com>

_pkgname=mpd-mpris
pkgname=mpd-mpris-git
pkgver=0.3.0.r3.gd7c9fbb
pkgrel=1
pkgdesc='An implementation of the MPRIS protocol for MPD'
arch=('x86_64')
url='https://github.com/natsukagami/mpd-mpris'
license=('MIT')
makedepends=('go>=1.9' 'git')
conflicts=('mpd-mpris')
provides=('mpd-mpris')
source=("$_pkgname::git+$url.git#branch=master"
        '0001-Upgrade-dbus-to-v5.0.4.patch')
sha256sums=('SKIP'
            '33aacdb4b06a8dcd303b8c9e22a94bb8af35b4d12632bf5c0f534b87ea163d08')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  patch -Np1 < "$srcdir/0001-Upgrade-dbus-to-v5.0.4.patch"
}

build() {
  cd "$_pkgname"
  go build \
    -o "$_pkgname" \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags \"${LDFLAGS}\"" \
    'cmd/mpd-mpris/main.go'
}

package() {
  cd "$_pkgname"
  install -D -m 0755 mpd-mpris "$pkgdir/usr/bin/mpd-mpris"
  install -D -m 0644 mpd-mpris.service "$pkgdir/usr/lib/systemd/user/mpd-mpris.service"
}
